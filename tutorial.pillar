{
   "metadata": {
     "title": "Roassal Tutorial",
     "author": "Leonel Merino",
     "author": "Alexandre Bergel",
     "year":"2016" 
   }
}

!SOME GENERAL COMMENTS
- We need to have the .mse file available somewhere. If we do that, we also need to have the source code. The tutorial has to indicates how to load .mse and how to set the root folder (to have the source code for methods)
- The tutorial can have a first part that simply play with some numbers. We can then show the basic of Moose query and let the students adapt their script to visualize source code
- Important question, will the student know the syntax of Pharo before?
- There is a problem with the figure. The reference says 0.1 ? That is weird.
- Also, the template of the .html is very ugly. With Agile Visualization, the HTML looks like *https://dl.dropboxusercontent.com/u/31543901/AgileVisualization/QuickStart/0101-QuickStart.html*. When I generate it, there is no css apparently. Any idea why?

!Roassal Tutorial

Roassal is a visualization engine largely used within the Pharo community. Roassal is used to visualize any set of data. In particular, it is often used to visualize large software source code base to assess their internal quality.

@@What is the goal of the tutorial? 

The following tutorial has been designed to introduce users to the Roassal visualization engine. At the end of the tutorial, users will be familiar to the basic API of Roassal as well with the domain-specific builders. Users will know how to visualize objects within a domain to enable exploration and analysis. They will visualize properties and relationships using the features offered by the engine such as shapes, layout, color, size, interactions (to name a few). Users should also be able to know where to find answers to further questions and to get deeper knowledge in the engine. Let's start.


!!Installation
Roassal is easy to install and the installation will not polute your file system. Simply download Moose as indicated on *http://moosetechnology.org*. You need three downloads: Moose 6.1 image, Pharo60.sources, and a virtual machine. The virtual machine depends on your operating system.

On Max OSX, you can open Moose (which contains Roassal) by drag and dropping the file moose-6.1.image on top of the virtual machine. You should obtain something like in Figure *openingMoose*.

+Opening moose>file://figures/openingMoose.png|label=openingMoose|width=40+

!!Hand's on: Basic Roassal API
In the following section we are going to incrementally use the basics of the Roassal API. In each step we (1) define a goal, (2) list required actions to achieve the goal, (3) show the changes to be made to the source code of the previous step (second step on), and (4) how the code of the visualization should look like after the changes (that you can copy/paste). 

!!!View, Shape, Layout and Interaction

@@goal Visualize the ==Collection== class hierarchy using a grid layout
#Create an RTView object
#Create elements using an ==RTBox== shape to represent each class
#Add the elements to the view
#Lay the elements out using an ==RTGridLayout==

[[[language=smalltalk
classes := Collection withAllSubclasses.
view := RTView new.
els := RTEllipse new
	elementsOn: classes.
view addAll: els.
RTGridLayout on: els.
els @ RTPopup.
els @ RTDraggable.
view @ RTDraggableView.
view
]]]


!!!Size, Color and Customize Popup

@@goal Map to the size and color of each box metrics of classes
#When creating the elements send the messages ==color:==, ==size:==
#Customize the ==text:== of the popup 

+file://figures/code_changes_1.png|width=100+

[[[language=smalltalk
classes := Collection withAllSubclasses.
view := RTView new.
els := RTEllipse new
	color: [ :e | Color gray: 1 - (e methods size / (classes max: [ :c | c methods size ])) ];
	size: [ :e | e numberOfLinesOfCode ];
	elementsOn: classes.
view addAll: els.
RTGridLayout on: els.
els @ (RTPopup new text: [ :e | e name , ' -> ' , e methods size asString ]).
els @ RTDraggable.
view @ RTDraggableView.
view
]]]


!!!Edges
@@goal Show the class hierarchy using edges and a tree layout.


#Add edges from the superclass to the subclass 
#Change the layout to a ==RTTreeLayout==

+file://figures/code_changes_2.png|width=100+


[[[language=smalltalk
classes := Collection withAllSubclasses.
view := RTView new.
els := RTEllipse new
	color: [ :e | Color gray: 1 - (e methods size / (classes max: [ :c | c methods size ])) ];
	size: [ :e | e numberOfLinesOfCode ];
	elementsOn: classes.
view addAll: els.
ei := RTEdgeBuilder new.
ei view: view.
ei objects: classes.
ei shape line color: Color gray.
ei connectFrom: #superclass to: #yourself.
RTTreeLayout on: els.
els @ (RTPopup new text: [ :e | e name , ' -> ' , e methods size asString ]).
els @ RTDraggable.
view @ RTDraggableView.
view
]]]

!!!Normalization
@@goal Adjust the visualization using normalization to map color and size, and apply a more scalable layout.
#Remove messages ==color:== and ==size:== sent during the creation of elements
#Add an ==RTMetricNormalizer== object to map the color and size
#Change the shape of elements to use ==RTEllipse==
#Push edges to the back of the circles

+file://figures/code_changes_3.png|width=100+


[[[language=smalltalk
classes := Collection withAllSubclasses.
view := RTView new.
els := RTEllipse new
	elementsOn: classes.
view addAll: els.
ei := RTEdgeBuilder new.
ei view: view.
ei objects: classes.
ei shape line color: Color gray.
ei connectFrom: #superclass to: #yourself.
RTMetricNormalizer new
	elements: els;
	normalizeSize: #numberOfLinesOfCode;
	normalizeColor: [ :e | e methods size ]
		using:
		{Color veryLightGray.
		Color red}
		using: #sqrt.
RTClusterLayout on: els.
els @ (RTPopup new text: [ :e | e name , ' -> ' , e methods size asString ]).
els @ RTDraggable.
view @ RTDraggableView.
view pushBackEdges.
view
]]]

!!!Bezier Edges
@@goal Add a different kind of edges to depict class dependencies
#Add bezier edges from each class to dependent classes

+file://figures/code_changes_4.png|width=100+


[[[language=smalltalk
classes := Collection withAllSubclasses.
view := RTView new.
els := RTEllipse new
	elementsOn: classes.
view addAll: els.
ei := RTEdgeBuilder new.
ei view: view.
ei objects: classes.
ei shape line color: Color gray.
ei connectFrom: #superclass to: #yourself.
RTMetricNormalizer new
	elements: els;
	normalizeSize: #numberOfLinesOfCode;
	normalizeColor: [ :e | e methods size ]
		using:
		{Color veryLightGray.
		Color red}
		using: #sqrt.
RTClusterLayout on: els.
eb := RTEdgeBuilder new.
eb view: view.
eb objects: classes.
eb shape
	bezierLineFollowing: #superclass;
	color: (Color blue alpha: 0.1).
eb connectFrom: #yourself toAll: #dependentClasses.
els @ (RTPopup new text: [ :e | e name , ' -> ' , e methods size asString ]).
els @ RTDraggable.
view @ RTDraggableView.
view pushBackEdges.
view
]]]

!!Does it scale to larger software?
Let's see. Now we are going to load a model of ""Tomcat"" into the Moose image and ajdust the visualization to analyze it. The model contains a description  of the main properties of the software such as classes, methods and invocations. Also, we are going to link the model to the software's source code, so we can inspect it within the visualization.  

!!!Set-up
#Download Tomcat's *model>http://scg.unibe.ch/download/merino/roassal-tutorial/tomcat-7.0.2.mse* and *sources>http://scg.unibe.ch/download/merino/roassal-tutorial/tomcat-7.0.2-src.zip*
#In Moose open from the menu the ''Moose panel'' and load the ''MSE'' model file
#Once the model is loaded, decompress the zip file that contains the sources. In the panel right-click the model and select ''utilities>Set root folder'', then specify the local folder that contains Tomcat's sources.

!!!Visualizing Tomcat's class hierarchy and dependencies
@@goal Show how the visualization performs on larger software (~1 min.).
#Adapt the data source to point to the classes within the Tomcat's model
#Modify the calls accordingly to obtain the dependent classes (other calls use similar signatures)

+file://figures/code_changes_5.png|width=100+


[[[language=smalltalk
classes := MooseModel root first allClasses.
view := RTView new.
els := RTEllipse new
	elementsOn: classes.
view addAll: els.
ei := RTEdgeBuilder new.
ei view: view.
ei objects: classes.
ei shape line color: Color gray.
ei connectFrom: #superclass to: #yourself.
RTMetricNormalizer new
	elements: els;
	normalizeSize: #numberOfLinesOfCode;
	normalizeColor: [ :e | e methods size ]
		using:
		{Color veryLightGray.
		Color red}
		using: #sqrt.
RTClusterLayout on: els.
eb := RTEdgeBuilder new.
eb view: view.
eb objects: classes.
eb shape
	bezierLineFollowing: #superclass;
	color: (Color blue alpha: 0.1).
eb connectFrom: #yourself toAll: [ :e | e methods flatCollect: #clientTypes ].
els @ (RTPopup new text: [ :e | e name , ' -> ' , e methods size asString ]).
els @ RTDraggable.
view @ RTDraggableView.
view pushBackEdges.
view
]]]

!!!Adapting the visualization for larger software
@@goal Distinguish Tomcat classes from libraries (Java core and external libs) and change the layout to ease the analysis of dependencies (~2.5 mins.)
#When creating elements use the message ==color:== to distinguish the source of a class (whether or not it belongs to Tomcat) and remove the previous definition from the normalizer
#Change the layout to ==RTForceBasedLayout==

+file://figures/code_changes_6.png|width=100+


[[[
classes := MooseModel root first allClasses.
view := RTView new.
els := RTEllipse new
	color:[:e| (MooseModel root first allModelClasses includes:e) 
			ifTrue:[Color blue] ifFalse:[Color red] ]; 
	elementsOn: classes.
ei := RTEdgeBuilder new.
ei view: view.
ei objects: classes.
ei shape line color: Color gray.
ei connectFrom: #superclass to: #yourself.

RTMetricNormalizer new
	elements: els;
	normalizeSize: #numberOfLinesOfCode.
RTForceBasedLayout on: els.

eb := RTEdgeBuilder new.
eb view: view.
eb objects: classes.
eb shape
	bezierLineFollowing: #superclass;
	color: (Color blue alpha: 0.1).
eb connectFrom: #yourself toAll: [ :e | e methods flatCollect: #clientTypes ].
els @ (RTPopup new text: [ :e | e name , ' -> ' , e methods size asString ]).
els @ RTDraggable.
view @ RTDraggableView.
view pushBackEdges.
view
]]]


!Exercise
Using Roassal API build a visualization that uses a Treemap to depict Tomcat's class hierarchy. Map to the area of the tiles the number of lines of code of the class they represent. Use two different colors to identify the classes that contain deprecated methods and the ones that might be tests. Hint: (1) you can assume that the name of test classes contain the ==Test== word. (2) Include in the visualization stubs of the Java core and libs classes (.e.g., ==Object==). (3) Notice that the model of the methods of a class contain its annotations.  

+file://figures/treemap.png|width=40+

@@goal Solution
[[[
| b |
b := RTTreeMapBuilder new.
b shape
	color: Color transparent;
	borderColor: Color black;
	if: [ :obj | obj isClass ]
		color: [ :cls | 
		(cls methods
			anySatisfy: [ :m | m annotationTypes contains: [ :e | e name = 'Deprecated' ] ])
			ifTrue: [ Color blue ]
			ifFalse: [ (cls name findString: 'Test') > 0
					ifTrue: [ Color green ]
					ifFalse: [ Color gray ] ] ].
b
	leafWeight: #numberOfLinesOfCode;
	explore: MooseModel root first allClasses anyOne superclass
		using: [ :e | e subInheritances collect: #subclass ].
^ b
]]]

!Resources
Links to resources for more detail on the Pharo language, the Moose plataform and the Roassal visualization engine. 

#*Pharo>http://pharo.org/*
#*Moose technology>http://www.moosetechnology.org/*
#*Deep into Pharo book>http://www.deepintopharo.com/*
#*Agile Visualization book>http://www.agilevisualization.com*
#Roassal examples shipped in the Pharo/Moose image.
#*Pharo users mailing lists>http://pharo.org/community*
